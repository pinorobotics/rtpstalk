plugins {
  id 'com.diffplug.spotless'
  id 'jacoco'
}

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

dependencies {
  testImplementation(project ':rtpstalk')
  testImplementation 'io.github.lambdaprime:pubsubtests:1.0-SNAPSHOT'
  testImplementation 'io.github.lambdaprime:id.xfunctiontests:1.0-SNAPSHOT'
  testImplementation "io.github.lambdaprime:id.opentelemetry-exporters-pack:2.0-SNAPSHOT"

  def junitVersion = "5.8.1"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-commons:1.7.0"
}

jacoco {
  // Java 21 support
  toolVersion = "0.8.10"
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport, jacocoTestCoverageVerification
  minHeapSize = "128m" // initial heap size
  maxHeapSize = "1400m" // maximum heap size

  testLogging {
    events "started", "failed"
  }
}

jacocoTestReport {
  // generate only HTML report
  reports {
    xml.required = false
    csv.required = false
    html.required = true
  }
  sourceDirectories.from = project(':rtpstalk').sourceSets.main.java.srcDirs
  classDirectories.from = project(':rtpstalk').sourceSets.main.java.classesDirectory
}

jacocoTestCoverageVerification {
  sourceDirectories.from = project(':rtpstalk').sourceSets.main.java.srcDirs
  classDirectories.from = project(':rtpstalk').sourceSets.main.java.classesDirectory
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude:  [
        'pinorobotics/rtpstalk/impl/spec/transport/io/exceptions/*',
        'pinorobotics/rtpstalk/exceptions/*',
      ])
    }))
  }
  violationRules {
    rule {
      enabled = true
      element = 'PACKAGE'
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.5
      }
    }
  }
}

// run spotless and format code before the build
testClasses.dependsOn spotlessApply
