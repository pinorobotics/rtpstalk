// include xfunction for updateVersion
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "io.github.lambdaprime:id.xfunction:23.0"
  }
}

plugins {
  id 'com.diffplug.spotless'
  // only if you build Java library
  id 'java-library'
}

apply plugin : 'eclipse'

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

dependencies {
  api "io.github.lambdaprime:id.xfunction:25.0-SNAPSHOT"
  api "io.github.lambdaprime:kineticstreamer:7.0"
  api "io.opentelemetry:opentelemetry-api:1.34.1"
}

configurations {
  customConfig.extendsFrom implementation
}

jar {
  manifest { 
    attributes 'Implementation-Version': "${rootProject.version}"
  }
}

javadoc {
  options.noTimestamp = true
  options.noQualifiers = ["all"]
  options.noTree = true
  options.noIndex = true
  options.noNavBar = true
  // ignore missing @return, @param etc
  options.addBooleanOption('Xdoclint:all,-missing', true)
  options.addStringOption('-show-packages', 'exported')
  exclude 'pinorobotics/rtpstalk/impl/**'

  // Gradle does not support exclude correctly:
  // https://github.com/gradle/gradle/issues/19726
  def sourceSetDirectories = sourceSets
    .main
    .java
    .sourceDirectories
    .getFiles()
    .stream()
    .map({f -> f.toString() })
    .collect(java.util.stream.Collectors.joining(":"))
  options.addStringOption("-source-path", sourceSetDirectories)
}

/**
 * If certain packages are not public and are exported only to specific modules
 * like "export foo.pkg.impl to bar_module;" then javadoc will generate documentation
 * for them as well (in example it will be foo.pkg.impl).
 * There is no way to ask javadoc to include documentation to only publicly available
 * packages (and drop foo.pkg.impl).
 * To fix this following task patches module-info during javadoc execution and removes
 * any "export ... to ...". After javadoc task completes the module-info restored back.
 */
javadoc.doFirst {
  var path = java.nio.file.Paths.get("${projectDir}/src/main/java/module-info.java")
  println "Patching ${path}"
  new id.xfunction.text.Substitutor()
    .withRegexpSupport()
    .withBackup()
    .substitute(path, Map.of(
      "(?s)(?m)exports \\S* to.*;", ""
    ));
}.doLast {
  var path = java.nio.file.Paths.get("${projectDir}/src/main/java/module-info.java")
  println "Restoring ${path}"
  id.xfunction.text.Substitutor.restoreBackup(path)
}

def packageDir = "$buildDir/package/${projectName}"

task createRelease(type: Zip) {
  archiveFileName = "$releaseFileName"
  destinationDirectory = file("$projectDir/release")
  from file("$packageDir/..")
}

task copyLibs(type: Copy) {
  from configurations.runtimeClasspath
  from jar.archiveFile
  into "${packageDir}/libs"
}

task createPackage(type: Copy, dependsOn: copyLibs) {
  from "${project.rootDir}/README.md"
  from "${project.rootDir}/LICENSE-2.0.txt"
  into packageDir
}

task cleanRelease(type: Delete) {
  delete "$packageDir"
  delete "$projectDir/release/*-SNAPSHOT.zip"
}

createRelease.dependsOn createPackage
assemble.dependsOn createRelease
clean.dependsOn cleanRelease

// run spotless and format code before the build
classes.dependsOn spotlessApply

task updateVersion() {
  // update version only when it is specified and non SNAPSHOT version is built
  // 'unspecified' is the default value for the version in Gradle if it is not set
  if (!version || version == "unspecified" || version.contains("SNAPSHOT")) return;
  println "Updating version to " + version
  new id.xfunction.text.Substitutor()
    .withRegexpSupport()
    .substitute(java.nio.file.Paths.get("${project.rootDir}/README.md"), Map.of(
      "io.github.pinorobotics:rtpstalk:[0-9.]+", "io.github.pinorobotics:rtpstalk:" + version
    ));
}
createRelease.dependsOn updateVersion

if (project.hasProperty('publish')) {

  // Initially it was taken from https://central.sonatype.org/publish/publish-gradle/
  // but since Gradle removed this plugin https://docs.gradle.org/7.0/userguide/upgrading_version_6.html#removal_of_the_legacy_maven_plugin
  // it was rewritten for new one

  task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from "README-javadoc.txt"
  }

  task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from "README-sources.txt"
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  apply plugin: 'maven-publish'

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = 'rtpstalk'
        groupId = 'io.github.pinorobotics'
        from components.java
        pom {
          name = 'rtpstalk'
          description = 'Java client for RTPS (Real-time Publish-Subscribe) protocol'
          url = 'http://pinoweb.freetzi.com/rtpstalk'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'aeon'
              email = 'aeon_flux@eclipso.ch'
            }
            developer {
              id = 'lambdaprime'
              email = 'intid@protonmail.com'
            }
          }

          scm {
            url = 'https://github.com/pinorobotics/rtpstalk'
          }
        }
      }
    }
    repositories {
      maven {
        credentials {
          username ossrhUsername
          password ossrhPassword
        }
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      }
    }    
  }

  if (project.hasProperty('signing.secretKeyRingFile')) {
    apply plugin: 'signing'

    signing {
      sign publishing.publications.mavenJava
    }
  }
}
