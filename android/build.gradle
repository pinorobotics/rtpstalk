plugins {
  id 'com.android.library' version '8.2.0' apply false
}

apply plugin: 'com.android.library'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(zzJavaVersion)
  }
}

dependencies {
  zzApiDependencies.split(',').each { api it }
}

android {
  namespace "${zzNamespace}.${zzName}"
  compileSdk Integer.parseInt(zzAndroidSdkVersion)
  compileOptions {
    sourceCompatibility zzJavaVersion
  }
  lintOptions {
    abortOnError false
  }
  defaultConfig {
    targetSdkVersion zzAndroidSdkVersion
    minSdkVersion zzAndroidSdkVersion
  }
}

// Android has java.logging classes available but not the java.logging module itself.
// Our module-info requires java.logging to be present as a module, which causes
// compilation for Android to break.
// To fix this, we try to exclude module-info from the compilation.
// Since it is not possible to exclude module-info through srcDirs:
// https://discuss.gradle.org/t/how-to-exclude-module-info-java-in-gradle-7/42816
// we copy all sources, except module-info, to a separate folder
task copySources(type: Copy) {
  from "../rtpstalk/src/main/java"
  into "src/main/java"
  exclude "**/module-info.java"
}
preBuild.dependsOn copySources

task cleanSrc(type: Delete) {
  delete "src/main/java"
}

clean.dependsOn cleanSrc
